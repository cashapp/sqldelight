plugins {
  alias(deps.plugins.shadow)
  alias(deps.plugins.kotlin.jvm)
  alias(deps.plugins.grammarKitComposer)
  alias(deps.plugins.publish)
}

grammarKit {
  intellijRelease.set(deps.versions.idea)
}

dependencies {
  api project(':dialects:sqlite-3-18')

  compileOnly project(':sqldelight-compiler:dialect')
  compileOnly deps.intellij.core
  compileOnly deps.intellij.lang

  testImplementation deps.intellij.core
  testImplementation deps.intellij.lang
  testImplementation deps.junit
  testImplementation deps.truth
  testImplementation project(':sqldelight-compiler:dialect')
  testImplementation deps.sqlPsiTestFixtures
}

tasks.getByName("shadowJar").configure {
  classifier = ''
  include "*.jar"
  include "app/cash/sqldelight/**"
  include 'META-INF/services/*'
}

tasks.jar.configure {
  // Prevents shadowJar (with classifier = '') and this task from writing to the same path.
  enabled = false
}

configurations {
  [apiElements, runtimeElements].each {
    it.outgoing.artifacts.removeIf { it.buildDependencies.getDependencies(null).contains(jar) }
    it.outgoing.artifact(shadowJar)
  }
}

artifacts {
  runtimeOnly(shadowJar)
  archives(shadowJar)
}

apply from: "$rootDir/gradle/gradle-mvn-push.gradle"
