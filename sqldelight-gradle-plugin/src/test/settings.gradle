dependencyResolutionManagement {
    versionCatalogs {
        libs {
            from(files("../../../gradle/libs.versions.toml"))
            String overwriteKotlinVersion = settings.ext.find("overwriteKotlinVersion")
            if (overwriteKotlinVersion != null) {
                version("kotlin", overwriteKotlinVersion)
            }

            // This version is set in the GradleRunner during test setup using the current (SNAPSHOT) version.
            // If you want to use the test projects as standalone samples, link the Gradle project in IntelliJ 
            // and overwrite this version.
            String sqldelightVersion = settings.sqldelightVersion

            library("sqldelight", "app.cash.sqldelight", "gradle-plugin")
                    .versionRef(version("sqldelightVersion", sqldelightVersion))
        }
    }

    repositories {
        maven {
            url "file://${rootDir}/../../../../build/localMaven"
        }
        mavenCentral()
        google()
        maven { url "https://www.jetbrains.com/intellij-repository/releases" }
        maven { url "https://cache-redirector.jetbrains.com/intellij-dependencies" }
    }
}
