apply plugin: 'kotlin'
apply plugin: 'java-gradle-plugin'

sourceCompatibility = JavaVersion.VERSION_1_7
targetCompatibility = JavaVersion.VERSION_1_7

test {
  testLogging {
    events "passed", "skipped", "failed"
    exceptionFormat "full"
    showExceptions true
    showStackTraces true
    showCauses true
  }
}

configurations {
  fixtureClasspath
  bundled
}

// Append any extra dependencies to the test fixtures via a custom configuration classpath. This
// allows us to apply additional plugins in a fixture while still leveraging dependency resolution
// and de-duplication semantics.
def metadata = tasks.getByName('pluginUnderTestMetadata')
metadata.setPluginClasspath(metadata.getPluginClasspath() + configurations.fixtureClasspath)

dependencies {
  implementation project(':sqldelight-core')
  implementation deps.kotlinStdLib
  implementation deps.sqlitePsi
  implementation deps.intellijGradleCore

  compileOnly gradleApi()
  compileOnly deps.androidPlugin

  testImplementation deps.junit
  testImplementation deps.truth

  bundled deps.intellijGradleCore

  fixtureClasspath deps.kotlinGradlePlugin
  fixtureClasspath deps.androidPlugin
}

jar {
  from {
    configurations.bundled.collect { it.isDirectory() ? it : zipTree(it) }
  }
}

apply from: "$rootDir/gradle/gradle-mvn-push.gradle"