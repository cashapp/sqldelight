plugins {
  alias(deps.plugins.android.library)
  alias(deps.plugins.kotlin.android)
  alias(deps.plugins.publish)
  alias(deps.plugins.dokka)
}

archivesBaseName = 'sqldelight-android-paging'

android {
  compileSdkVersion deps.versions.compileSdk.get() as int

  lintOptions {
    textReport true
    textOutput 'stdout'
  }

  defaultConfig {
    minSdkVersion deps.versions.minSdk.get() as int
  }

  buildFeatures {
    buildConfig = false
  }

  testOptions {
    unitTests.includeAndroidResources = true
  }
}

dependencies {
  // workaround for https://youtrack.jetbrains.com/issue/KT-27059
  api "${project.property("GROUP")}:runtime-jvm:${project.property("VERSION_NAME")}"
  api deps.androidx.paging.common

  testImplementation project(':drivers:android-driver')
  testImplementation deps.truth
  testImplementation deps.junit
  testImplementation deps.androidx.test.core
  testImplementation deps.robolectric
}

// workaround for https://youtrack.jetbrains.com/issue/KT-27059
configurations.all {
    resolutionStrategy.dependencySubstitution {
        substitute module("${project.property("GROUP")}:runtime-jvm:${project.property("VERSION_NAME")}") with project(':runtime')
    }
}

apply from: rootProject.file('gradle/gradle-mvn-push.gradle')
