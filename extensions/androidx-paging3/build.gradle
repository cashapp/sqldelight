plugins {
  alias(libs.plugins.kotlin.multiplatform)
  alias(libs.plugins.publish)
  alias(libs.plugins.dokka)
  id("app.cash.sqldelight.toolchain.runtime")
}

archivesBaseName = 'sqldelight-androidx-paging3'

kotlin {
  ios()
  jvm()
  js {
    browser()
  }

  sourceSets {
    commonMain {
      dependencies {
        api projects.runtime
        api libs.app.cash.paging.common
        implementation libs.kotlin.coroutines.core
      }
    }
    commonTest {
      dependencies {
        implementation libs.kotlin.test
        implementation libs.kotlin.coroutines.test
        implementation libs.stately.concurrency
      }
    }
    iosTest {
      dependencies {
        implementation projects.drivers.nativeDriver
        implementation libs.app.cash.paging.runtime
      }
    }
    jvmTest {
      dependencies {
        implementation projects.drivers.sqliteDriver
        implementation libs.androidx.paging3.runtime
      }
    }
    jsTest {
      dependencies {
        implementation projects.drivers.sqljsDriver
      }
    }
  }

  configure([targets.iosX64, targets.iosArm64]) {
    binaries.configureEach {
      linkerOpts += ["-lsqlite3"]
    }
  }
}

apply from: "$rootDir/gradle/gradle-mvn-push.gradle"
