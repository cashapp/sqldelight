plugins {
  alias(libs.plugins.kotlin.multiplatform)
  alias(libs.plugins.publish)
  alias(libs.plugins.dokka)
}

archivesBaseName = 'sqldelight-androidx-paging3'

kotlin.jvmToolchain {
  languageVersion.set(JavaLanguageVersion.of(8))
  vendor.set(JvmVendorSpec.AZUL)
}

kotlin {
  ios()
  jvm()
  js {
    browser()
  }

  sourceSets {
    commonMain {
      dependencies {
        api project(':runtime')
        api libs.app.cash.paging.common
        implementation libs.kotlin.coroutines.core
      }
    }
    commonTest {
      dependencies {
        implementation libs.kotlin.test
        implementation libs.kotlin.coroutines.test
        implementation libs.stately.concurrency
      }
    }
    iosTest {
      dependencies {
        implementation project(':drivers:native-driver')
        implementation libs.app.cash.paging.runtime
      }
    }
    jvmTest {
      dependencies {
        implementation project(':drivers:sqlite-driver')
        implementation libs.androidx.paging3.runtime
      }
    }
    jsTest {
      dependencies {
        implementation project(':drivers:sqljs-driver')
      }
    }
  }

  configure([targets.iosX64, targets.iosArm64]) {
    binaries.configureEach {
      linkerOpts += ["-lsqlite3"]
    }
  }
}

apply from: "$rootDir/gradle/gradle-mvn-push.gradle"
