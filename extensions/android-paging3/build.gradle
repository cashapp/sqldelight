plugins {
  id("org.jetbrains.kotlin.multiplatform")
  alias(libs.plugins.publish)
  alias(libs.plugins.dokka)
}

archivesBaseName = 'sqldelight-androidx-paging3'

kotlin {
  ios()
  jvm()

  sourceSets {
    commonMain {
      dependencies {
        api project(':runtime')
        api libs.app.cash.paging.common
        implementation libs.kotlin.coroutines.core
      }
    }
    commonTest {
      dependencies {
        implementation libs.kotlin.test
        implementation libs.kotlin.coroutines.test
        implementation libs.stately.concurrency
      }
    }
    iosTest {
      dependencies {
        implementation project(':drivers:native-driver')
        implementation libs.app.cash.paging.runtime
      }
    }
    jvmTest {
      dependencies {
        implementation project(':drivers:sqlite-driver')
        implementation libs.androidx.paging3.runtime
      }
    }
  }

  configure([targets.iosX64, targets.iosArm64]) {
    compilations.test {
      kotlinOptions.freeCompilerArgs += ['-linker-options', '-lsqlite3 -L/usr/lib/x86_64-linux-gnu -L/usr/lib -L/usr/lib64']
    }
  }
}

tasks.withType(org.jetbrains.kotlin.gradle.targets.native.tasks.KotlinNativeSimulatorTest) {
  deviceId = "iPhone 14"
}

apply from: "$rootDir/gradle/gradle-mvn-push.gradle"
