plugins {
  alias(libs.plugins.publish)
  alias(libs.plugins.dokka)
  alias(libs.plugins.binaryCompatibilityValidator)
  id("app.cash.sqldelight.multiplatform")
  id("app.cash.sqldelight.toolchain.runtime")
}

// https://youtrack.jetbrains.com/issue/KTIJ-14471
sourceSets {
  main
}

apiValidation {
  ignoredPackages += ["app.cash.sqldelight.internal"]
}

kotlin {
  targetHierarchy.default {
    it.group("testableNative") {
      // no withApple: https://github.com/cashapp/sqldelight/issues/4257
      it.withIos()
      it.withTvos()
      it.withMacos()
      it.withWatchosArm32()
      it.withWatchosArm64()
      it.withWatchosX64()
      it.withWatchosSimulatorArm64()

      it.withLinuxX64()
      it.withMingw()
    }
  }

  sourceSets {
    commonMain {
    }
    commonTest {
      dependencies {
        implementation libs.kotlin.test
      }
    }
    jvmTest {
      dependencies {
        implementation libs.kotlin.test.junit
        implementation libs.stately.collections
      }
    }
    testableNativeTest {
      dependencies {
        implementation libs.stately.concurrency
        implementation libs.stately.collections
      }
    }
  }
}

apply from: "$rootDir/gradle/gradle-mvn-push.gradle"
