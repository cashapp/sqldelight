/*
 * Copyright (C) 2017 Square, Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
{
  elementTypeHolderClass="com.squareup.sqldelight.core.SqlDelightTypes"
  psiPackage = "com.squareup.sqldelight.core.psi"
  psiImplPackage = "com.squareup.sqldelight.core.psi.impl"

  overrides="com.alecstrong.sqlite.psi.core.SqliteParser"
  elementTypeClass = "com.alecstrong.sqlite.psi.core.SqliteElementType"
  psiClassPrefix = "SqlDelight"

  parserImports=[
    // If you want to use a token from the core grammar it must be statically imported.
    "static com.alecstrong.sqlite.psi.core.psi.SqliteTypes.DEFAULT"
    "static com.alecstrong.sqlite.psi.core.psi.SqliteTypes.VALUES"
    "static com.alecstrong.sqlite.psi.core.psi.SqliteTypes.JAVADOC"
  ]
}
sup ::= column_def | sql_stmt_list | type_name | insert_stmt_values

sql_stmt_list ::= import_stmt_list ( stmt_identifier {statement} ) * {
  implements="com.alecstrong.sqlite.psi.core.psi.SqliteSqlStmtList"
  extends="com.alecstrong.sqlite.psi.core.psi.impl.SqliteSqlStmtListImpl"
  override = true
}
column_def ::= {column_name} type_name [ 'AS' ('@' annotation) * java_type_name ] ( {column_constraint} ) * {
  implements=[
    "com.alecstrong.sqlite.psi.core.psi.SqliteColumnDef";
    "com.squareup.sqldelight.core.lang.psi.TypedColumn"
  ]
  extends="com.squareup.sqldelight.core.lang.psi.ColumnDefMixin"
  override = true
}
type_name ::= ( 'TEXT' | 'BLOB' | 'INTEGER' | 'REAL' ) {
  implements=[
    "com.alecstrong.sqlite.psi.core.psi.SqliteTypeName";
    "com.squareup.sqldelight.core.lang.psi.TypedColumn"
  ]
  extends="com.squareup.sqldelight.core.lang.psi.SqliteTypeMixin"
  override = true
}
insert_stmt_values ::= ( VALUES ( {bind_expr} | {values_expression}
  ( ',' {values_expression} ) * ) | {compound_select_stmt} | DEFAULT VALUES ) {
  implements="com.alecstrong.sqlite.psi.core.psi.SqliteInsertStmtValues"
  extends="com.squareup.sqldelight.core.lang.psi.InsertStmtValuesMixin"
  override = true
}

// New rules.
import_stmt ::= 'import' java_type ';' {
  extends="com.squareup.sqldelight.core.lang.psi.ImportStmtMixin"
}
import_stmt_list ::= import_stmt *
stmt_identifier ::= [ JAVADOC ] ( {identifier} ':' ) ? {
  extends="com.squareup.sqldelight.core.lang.psi.StmtIdentifierMixin"
  implements="com.squareup.sqldelight.core.lang.psi.StmtIdentifier"
}
annotation ::= java_type ( '(' annotation_value ')'
                         | '(' {identifier} '=' annotation_value ( ',' {identifier} '=' annotation_value )* ')'
                         )?
annotation_value ::= java_type_name '::class'
                   | {string_literal}
                   | {numeric_literal}
                   | '[' annotation_value ( ',' annotation_value ) * ']'
                   | annotation
java_type_name ::= 'Boolean'
            | 'Short'
            | 'Int'
            | 'Integer'
            | 'Long'
            | 'Float'
            | 'Double'
            | 'String'
            | 'ByteArray'
            | parameterized_java_type
parameterized_java_type ::= java_type ( ( '<' java_type_name ( ',' java_type_name )* '>' )
                          | ( '<' ( java_type_name ',' )* java_type '<' java_type_name2 ( ',' java_type_name2 )* '>>' ) )?
java_type_name2 ::= java_type
java_type ::= ( {identifier} '.' )* {identifier} {
  mixin="com.squareup.sqldelight.core.lang.psi.JavaTypeMixin"
}
