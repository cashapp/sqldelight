plugins {
  alias(libs.plugins.android.library)
  alias(libs.plugins.kotlin.android)
  alias(libs.plugins.publish)
  alias(libs.plugins.dokka)
}
archivesBaseName = 'sqldelight-android-driver'

android {
  compileSdk libs.versions.compileSdk.get() as int
  namespace = "com.squareup.sqldelight.driver.android"

  lint {
    textReport true
    textOutput file("$reportsDir/lint-results.txt")
  }

  defaultConfig {
    minSdk libs.versions.minSdk.get() as int
  }

  buildFeatures {
    buildConfig = false
  }

  testOptions  {
    unitTests {
      includeAndroidResources = true
    }
  }
}

dependencies {
  // workaround for https://youtrack.jetbrains.com/issue/KT-27059
  api "${project.property("GROUP")}:runtime-jvm:${project.property("VERSION_NAME")}"
  api libs.androidx.sqlite

  implementation libs.androidx.sqliteFramework

  testImplementation project(':drivers:driver-test')
  testImplementation libs.junit
  testImplementation libs.androidx.test.core
  testImplementation libs.robolectric
}

// workaround for https://youtrack.jetbrains.com/issue/KT-27059
configurations.all {
    resolutionStrategy.dependencySubstitution {
        substitute module("${project.property("GROUP")}:runtime-jvm:${project.property("VERSION_NAME")}") with project(':runtime')
    }
}

apply from: rootProject.file('gradle/gradle-mvn-push.gradle')

// https://github.com/Kotlin/dokka/issues/2472
afterEvaluate {
  tasks.named("javaDocReleaseGeneration") { enabled(false) }
}
