apply plugin: 'org.jetbrains.kotlin.multiplatform'

kotlin {
  sourceSets {
    commonMain {
      dependencies {
        api project (':runtime')
      }
    }
    commonTest {
      dependencies {
        implementation deps.kotlin.test.common
        implementation deps.testhelp
      }
    }
    nativeMain {
      dependencies {
        api deps.sqliter
        implementation deps.stately.core
      }
    }
    nativeTest {
      dependencies {
        implementation project(':drivers:driver-test')
      }
    }
  }

  // Workaround IDEA indexing issues, by binding nativeMain and nativeTest source sets to the iosX64 target.
  def isIdeaActive = System.getProperty("idea.active") == "true"

  targets {
    targetFromPreset(presets.iosX64, isIdeaActive ? 'native' : 'iosX64')
    targetFromPreset(presets.iosArm32, 'iosArm32')
    targetFromPreset(presets.iosArm64, 'iosArm64')
    targetFromPreset(presets.tvosX64, 'tvosX64')
    targetFromPreset(presets.tvosArm64, 'tvosArm64')
    targetFromPreset(presets.watchosX86, 'watchosX86')
    targetFromPreset(presets.watchosArm32, 'watchosArm32')
    targetFromPreset(presets.watchosArm64, 'watchosArm64')
    targetFromPreset(presets.macosX64, 'macosX64')
    targetFromPreset(presets.mingwX64, 'mingw') {
      compilations.main.kotlinOptions.freeCompilerArgs += ["-linker-options", "-Lc:\\msys64\\mingw64\\lib"]
      compilations.test.kotlinOptions.freeCompilerArgs += ["-linker-options", "-L$projectDir\\libs".toString()]
    }
  }

  configure([targets.iosArm32, targets.iosArm64, targets.tvosX64, targets.tvosArm64, targets.watchosX86, targets.watchosArm32, targets.watchosArm64, targets.macosX64, targets.mingw] + (isIdeaActive ? [targets.native] : [targets.iosX64])) {
    compilations.main.source(sourceSets.nativeMain)
    compilations.test.source(sourceSets.nativeTest)
    compilations.test {
      kotlinOptions.freeCompilerArgs += ["-linker-options", "-lsqlite3"]
    }
  }
}

apply from: "$rootDir/gradle/gradle-mvn-push.gradle"
