apply plugin: 'org.jetbrains.kotlin.multiplatform'

kotlin {

  js {
    useCommonJs()
    nodejs {
      testTask {
        useMocha()
      }
    }
    browser {
      testTask {
        useMocha()
      }
    }
  }

  sourceSets {
    commonMain {
      dependencies {
        implementation deps.kotlin.stdlib.common
        implementation deps.kotlin.coroutines.common
        api project (':sqldelight-runtime')
      }
    }
    commonTest {
      dependencies {
        implementation deps.kotlin.test.common
      }
    }
    jsMain {
      dependencies {
        implementation deps.kotlin.coroutines.js
        api npm(deps.sqljs, versions.sqljs)
      }
    }
    jsTest {
      dependencies {
        //implementation project(':drivers:driver-test')
        implementation deps.kotlin.test.js
      }
    }
  }
}

apply from: "$rootDir/gradle/gradle-mvn-mpp-push.gradle"

publishing {
  publications.all {
    // Rewrite all artifacts from using the project name to just 'runtime'.
    artifactId = artifactId.replace(project.name, 'sqljs-driver')
  }
}

afterEvaluate {
  // Alias the task names we use elsewhere to the new task names.
  tasks.register('install') {
    dependsOn('publishKotlinMultiplatformPublicationToMavenLocal')
  }
  tasks.register('installLocally') {
    dependsOn 'publishMetadataPublicationToTestRepository'
    dependsOn 'publishKotlinMultiplatformPublicationToTestRepository'
    dependsOn 'publishJsPublicationToTestRepository'
  }
  // NOTE: We do not alias uploadArchives because CI runs it on Linux and we only want to run it on Mac OS.
  //tasks.register('uploadArchives').dependsOn('publishKotlinMultiplatformPublicationToMavenRepository')
}

// TODO work around for https://youtrack.jetbrains.com/issue/KT-33579
task fixPathProblem(type: Copy) {
    from "$rootDir/build/js/packages/sqldelight-sqljs-driver-test/kotlin"
    into "$rootDir/build/js/packages/sqldelight-sqljs-driver-test/node_modules/kotlin"
}
task fixSourceMapProblem(type: Copy) {
    from "$rootDir/build/js/node_modules/kotlin-test-nodejs-runner/kotlin-nodejs-source-map-support.js"
    into "$rootDir/build/js/packages/sqldelight-sqljs-driver-test/"
}
tasks["jsTestClasses"].dependsOn("fixSourceMapProblem", "fixPathProblem")
