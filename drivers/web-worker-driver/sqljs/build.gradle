import dev.petuska.npm.publish.extension.domain.NpmDependency

plugins {
  alias(libs.plugins.npmPublish)
  alias(libs.plugins.dokka)
}

npmPublish {
  organization.set("cashapp")
  packages {
    register("js") {
      packageName.set("sqldelight-sqljs-worker")
      files {
        from("sqljs.worker.js")
      }

      packageJson {
        license.set("Apache-2.0")
        repository {
          type.set("git")
          url.set("git+https://github.com/cashapp/sqldelight.git")
        }
        description.set("A SQL.js implementation of SQLDelight's web-worker-driver")
        keywords.set(["sqljs", "sqldelight", "sql"])
        bugs {
          url.set("https://github.com/cashapp/sqldelight/issues")
        }
        homepage.set("https://github.com/cashapp/sqldelight#readme")
      }

      it.dependencies {
        register("sql.js") {
          version.set("^1.8.0")
          type.set(NpmDependency.Type.PEER)
        }
      }
    }
  }
}

rootProject.tasks.named("kotlinNpmInstall").configure {
  dependsOn(tasks.named("assembleJsPackage"))
}

// Dokka forces us to apply the plugin to this module even though there's no code
tasks.named("dokkaHtmlPartial") {
  enabled = false
}
