apply plugin: "com.vanniktech.maven.publish"
apply plugin: "org.jetbrains.dokka"

tasks.named("dokkaGfm").configure {
  outputDirectory = file("$rootDir/docs/1.x")
}

publishing {
    repositories {
        maven {
            name = "installLocally"
            url = "${rootProject.buildDir}/localMaven"
        }
    }

    publications {
    // Specify relocation POM
      relocation(MavenPublication) {
        pom {
          // Old artifact coordinates
          groupId = "com.squareup.sqldelight"
          artifactId = POM_ARTIFACT_ID
          version = "2.0.0"

          distributionManagement {
            relocation {
              // New artifact coordinates
              groupId = "app.cash.sqldelight"
              artifactId = POM_ARTIFACT_ID
              version = "2.0.0"
              message = "groupId has been changed"
            }
          }
        }
      }
  }
}

tasks.withType(AbstractArchiveTask).configureEach {
    setPreserveFileTimestamps false
    setReproducibleFileOrder true
}
