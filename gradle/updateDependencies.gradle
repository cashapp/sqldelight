apply plugin: 'de.undercouch.download'


// TODO This touches the filesystem during the gradle configuration phase. Move inside a task.
def downloadDir = new File(project.rootDir, "lib/download")
downloadDir.mkdirs()

def intellijCoreZip = new File(downloadDir, "intellij-core-${versions.idea}.zip")

task downloadIntellij(type: Download) {
  src "https://www.jetbrains.com/intellij-repository/releases/com/jetbrains/intellij/idea/ideaIC/" +
          "${versions.idea}/ideaIC-${versions.idea}.zip"
  dest intellijCoreZip
  onlyIfNewer true
}

task unzipIntellij(type: Copy) {
  from zipTree(intellijCoreZip)
  into new File("lib/intellij-core")
  dependsOn(downloadIntellij)
}

// Make every other task depend on 'unzipIntellij'.
// matching() and all() are "live" so any tasks declared after this line will also depend on 'unzipIntellij'
tasks.matching { it.name != 'unzipIntellij' && it.name != 'downloadIntellij'}.all { Task task ->
  task.dependsOn unzipIntellij
}
